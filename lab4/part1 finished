               .equ      EDGE_TRIGGERED,    0x1
               .equ      LEVEL_SENSITIVE,   0x0
               .equ      CPU0,              0x01    		// bit-mask; bit 0 represents cpu0
               .equ      ENABLE,            0x1

			   .equ		 KEY_BASE,  		0xFF200050
               .equ      KEY0,              0b0001
               .equ      KEY1,              0b0010
               .equ      KEY2,              0b0100
               .equ      KEY3,              0b1000
			   
			   .equ      HEX_BASE,          0xFF200020
			   .equ		 BLANK,             0b00000000
			   .equ		 ZERO,				0b00111111
			   .equ		 ONE,				0b00000110
			   .equ		 TWO,				0b01011011
			   .equ		 THREE,				0b01001111

               .equ      IRQ_MODE,          0b10010			// Interrupt Mode
               .equ      SVC_MODE,          0b10011			// Supervisor Mode

               .equ      INT_ENABLE,        0b01000000		// CPSR "I" bit = 0
               .equ      INT_DISABLE,       0b11000000		// CPSR "I" bit = 1
/*********************************************************************************
 * Initialize the exception vector table
 ********************************************************************************/
                .section .vectors, "ax"		// set the exception vector table

                B        _start             // reset vector
                .word    0                  // undefined instruction vector
                .word    0                  // software interrrupt vector
                .word    0                  // aborted prefetch vector
                .word    0                  // aborted data vector
                .word    0                  // unused vector
                B        IRQ_HANDLER        // IRQ interrupt vector
                .word    0                  // FIQ interrupt vector

/*********************************************************************************
 * Main program
 ********************************************************************************/
                .text
                .global  _start
_start:         /* Set up stack pointers for IRQ and SVC processor modes */
                
				/* First Interrupt Mode with interrupts disabled */
                MOV      R1, #0b11010010 			// Mode = IRQ: #0b11010010
				MSR		 CPSR_c, R1					// change to IRQ mode
											   		// MSR -- move into status register command
				LDR		 SP, =0x40000				// set IRQ stack pointer, we normally put it 0x40000
				/* Change to Supervisor Mode with interrupts disabled */
				MOV		 R1, #0b11010011        	// MODE = SVC: #0b11010011
				MSR 	 CPSR_c, R1
				LDR		 SP, =0x20000				// set SVC stack pointer, we normally put it 0x20000
																			
                /*configure the ARM generic interrupt controller*/
				BL       CONFIG_GIC
				
				/*Configure the KEY pushbutton port to generate interrupts*/
                LDR		 R0, =KEY_BASE				// set interrupt mask register all 1
				MOV		 R1, #0xF					// so that all keys can cause an interrrupt
				STR		 R1, [R0, #0x8]				// interrupt mask register is KEY_BASE+8
													
                /*set up values of HEX3-0*/
				LDR		 R0, =HEX_BASE
				MOV 	 R1, #BLANK
				STR		 R1, [R0]					// blank HEX3-0

                /*enable IRQ interrupts in the processor*/
                MOV      R0, #0b01010011 			// Interrupt enabled, MODE = SVC
				MSR		 CPSR_c, R0

/*main program*/
IDLE:			LDR	     R4, #KEYVAL
				LDR		 R5, =HEX_BASE
				LDR		 R3, [R5]				// load HEX pattern
				CMP		 R4, #1
				BEQ		 DISPLAY0
				CMP		 R4, #2
				BEQ		 DISPLAY1
				CMP		 R4, #4
				BEQ		 DISPLAY2
				CMP		 R4, #8
				BEQ		 DISPLAY3
				B        IDLE                    // main program simply idles				
/*toggle between zero and blank*/
DISPLAY0:		LDR		 R3, [R5]
				LDRB	 R0, [R5]
				CMP		 R0, #0
				BNE		 CLEAR0
				MOV		 R0, #ZERO
				ORR		 R0, R3
				STR	     R0, [R5]
				B		 DONE_DISPLAY
CLEAR0:			AND		 R0, R3, #0xFFFFFF00
				STR	     R0, [R5]
				B		 DONE_DISPLAY
/*toggle between one and blank*/
DISPLAY1: 		LDR		 R3, [R5]
				LDRB	 R0, [R5,#1]
				CMP		 R0, #0
				BNE		 CLEAR1
				MOV		 R0, #ONE
				LSL		 R0, #8
				ORR		 R0, R3
				STR	     R0, [R5]
				B		 DONE_DISPLAY
CLEAR1:			AND		 R0, R3, #0xFFFF00FF
				STR 	 R0, [R5]
				B		 DONE_DISPLAY
/*toggle between two and blank*/
DISPLAY2: 		LDR		 R3, [R5]
				LDRB	 R0, [R5, #2]
				CMP		 R0, #0
				BNE		 CLEAR2
				MOV		 R0, #TWO
				LSL		 R0, #16
				ORR		 R0, R3
				STR		 R0, [R5]
				B		 DONE_DISPLAY
CLEAR2:		    AND		 R0, R3, #0xFF00FFFF
				STR		 R0, [R5]
				B		 DONE_DISPLAY
/*toggle between three and blank*/
DISPLAY3: 		LDR		 R3, [R5]
				LDRB	 R0, [R5, #3]
				CMP		 R0, #0
				BNE		 CLEAR3
				MOV		 R0, #THREE
				LSL		 R0, #24
				ORR		 R0, R3
				STR		 R0, [R5]
				B		 DONE_DISPLAY
CLEAR3:		 	AND		 R0, R3, #0x00FFFFFF
				STR		 R0, [R5]
				B		 DONE_DISPLAY			
DONE_DISPLAY:   MOV		 R0, #0
				STR	 	 R0, KEYVAL
				B        IDLE
				
				.text
KEYVAL:			.word    0
			
			
				.global  IRQ_HANDLER
IRQ_HANDLER:
                PUSH     {R0-R7, LR}
    
                /* Read the ICCIAR in the CPU interface */
                LDR      R4, =0xFFFEC100
                LDR      R5, [R4, #0x0C]         // read the interrupt ID

CHECK_KEYS:		CMP      R5, #73
UNEXPECTED:     BNE      UNEXPECTED              // if not recognized, stop here
    
                BL       KEY_ISR				 // KEYS cause the interrupt
EXIT_IRQ:
                /* Write to the End of Interrupt Register (ICCEOIR) */
                STR      R5, [R4, #0x10]
    
                POP      {R0-R7, LR}
                SUBS     PC, LR, #4

/*****************************************************0xFF200050***********************************
 * Pushbutton - Interrupt Service Routine                                
 *                                                                          
 * This routine checks which KEY(s) have been pressed. It writes to HEX3-0
 ***************************************************************************************/
                .global  KEY_ISR
KEY_ISR:		
				MOV		 R3, #0
                LDR      R0, =0xFF200050          // pushbutton KEY port base address
            	LDR      R2, [R0, #0xC]        // read Edgecapture
				STR		 R2, KEYVAL			   // KEYVAL now has which KEY cause the interrupt
             	STR      R2, [R0, #0xC]        // clear the interrupt
 			    MOV      PC, LR

/* 
 * Configure the Generic Interrupt Controller (GIC)
*/
                .global  CONFIG_GIC
CONFIG_GIC:
                PUSH     {LR}
                /* Enable the KEYs interrupts */
                MOV      R0, #73
                MOV      R1, #CPU0
                /* CONFIG_INTERRUPT (int_ID (R0), CPU_target (R1)); */
                BL       CONFIG_INTERRUPT

                /* configure the GIC CPU interface */
                LDR      R0, =0xFFFEC100        // base address of CPU interface
                /* Set Interrupt Priority Mask Register (ICCPMR) */
                LDR      R1, =0xFFFF            // enable interrupts of all priorities levels
                STR      R1, [R0, #0x04]
                /* Set the enable bit in the CPU Interface Control Register (ICCICR). This bit
                 * allows interrupts to be forwarded to the CPU(s) */
                MOV      R1, #1
                STR      R1, [R0]
    
                /* Set the enable bit in the Distributor Control Register (ICDDCR). This bit
                 * allows the distributor to forward interrupts to the CPU interface(s) */
                LDR      R0, =0xFFFED000
                STR      R1, [R0]    
    
                POP      {PC}
/* 
 * Configure registers in the GIC for an individual interrupt ID
 * We configure only the Interrupt Set Enable Registers (ICDISERn) and Interrupt 
 * Processor Target Registers (ICDIPTRn). The default (reset) values are used for 
 * other registers in the GIC
 * Arguments: R0 = interrupt ID, N
 *            R1 = CPU target
*/
CONFIG_INTERRUPT:
                PUSH     {R4-R5, LR}
    
                /* Configure Interrupt Set-Enable Registers (ICDISERn). 
                 * reg_offset = (integer_div(N / 32) * 4
                 * value = 1 << (N mod 32) */
                LSR      R4, R0, #3               // calculate reg_offset
                BIC      R4, R4, #3               // R4 = reg_offset
                LDR      R2, =0xFFFED100
                ADD      R4, R2, R4               // R4 = address of ICDISER
    
                AND      R2, R0, #0x1F            // N mod 32
                MOV      R5, #1                   // enable
                LSL      R2, R5, R2               // R2 = value

                /* now that we have the register address (R4) and value (R2), we need to set the
                 * correct bit in the GIC register */
                LDR      R3, [R4]                 // read current register value
                ORR      R3, R3, R2               // set the enable bit
                STR      R3, [R4]                 // store the new register value

                /* Configure Interrupt Processor Targets Register (ICDIPTRn)
                  * reg_offset = integer_div(N / 4) * 4
                  * index = N mod 4 */
                BIC      R4, R0, #3               // R4 = reg_offset
                LDR      R2, =0xFFFED800
                ADD      R4, R2, R4               // R4 = word address of ICDIPTR
                AND      R2, R0, #0x3             // N mod 4
                ADD      R4, R2, R4               // R4 = byte address in ICDIPTR

                /* now that we have the register address (R4) and value (R2), write to (only)
                 * the appropriate byte */
                STRB     R1, [R4]
    
                POP      {R4-R5, PC}

                .end   
